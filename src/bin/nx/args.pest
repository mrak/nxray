on = { "+" }
off = { "-" }
protocol = { "tcp" | "udp" | "icmp" | "ethernet" }
pcap = { "pcap" }
protocol_opt = ${ (on | off)? ~ protocol }
destination = @{ "@" }
source = @{ "^" }
direction = ${ source | destination }
address = ${ port_opt | ipv4_address | ipv6_address | mac_address }
anchor_address = ${ direction ~ address }
packet_between = @{ address ~ "," ~ address }
packet_anchored = @{ anchor_address ~ anchor_address }
port = @{ ASCII_DIGIT{1,5} }
port_range_lower = ${ "-" ~ port }
port_range_upper = ${ port ~ "-" }
port_range_bounded = ${ port ~ "-" ~ port }
port_range = _{ port_range_lower | port_range_bounded | port_range_upper }
port_list = ${ port ~ ("," ~ port)+ }
port_opt = ${ ":" ~ (port_range | port_list | port) }
mask = @{ "/" ~ ASCII_DIGIT+ }
ipv4_ip = @{ (ASCII_DIGIT{1,3} ~ "."){3} ~ ASCII_DIGIT{1,3} }
ipv4_address = @{ ipv4_ip ~ mask? ~ port_opt? }
ipv6_address_byte = @{ "::" | ":"? ~ ASCII_HEX_DIGIT{1,4} }
ipv6_ip = @{ "[" ~ ipv6_address_byte{1,8} ~ "]" }
ipv6_address = @{ ipv6_ip ~ mask? ~ port_opt? }
mac_address = @{ (ASCII_HEX_DIGIT{2} ~ ":"){5} ~ ASCII_HEX_DIGIT{2} }
interface = @{ (ASCII_ALPHANUMERIC | "-")+ }
argument = { SOI ~ (pcap
  | protocol_opt
  | address
  | anchor_address
  | packet_anchored
  | packet_between
  | interface
  ) ~ EOI }
